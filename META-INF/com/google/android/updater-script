if
   file_getprop("/tmp/aroma-data/options.prop","option_install_type") == "1"
then
  ui_print("@ Performing a quick install");
else
  ui_print("@ Performing a customized install");
endif;

ui_print("@ Mounting partitions");

run_program("/sbin/mount", "/dev/block/stl9 /system");
run_program("/sbin/mount", "/dev/block/stl10 /data");
run_program("/sbin/mount", "/dev/block/stl11 /cache");

show_progress(0.0, 0);
ui_print("@ Copying files required to format");
package_extract_file("tools/fsck.ext4", "/tmp/fsck.ext4");
package_extract_file("tools/tune2fs.ext4", "/tmp/tune2fs.ext4");
package_extract_file("tools/mkfs.ext4", "/tmp/mkfs.ext4");
package_extract_file("etc/mke2fs.conf", "/etc/mke2fs.conf");
package_extract_file("tools/data-backup", "/tmp/data-backup");
package_extract_file("tools/data-restore", "/tmp/data-restore");
package_extract_file("tools/busybox", "/tmp/busybox");
set_perm(0, 0, 0755, "/tmp/mkfs.ext4");
set_perm(0, 0, 0755, "/tmp/fsck.ext4");
set_perm(0, 0, 0755, "/tmp/tune2fs.ext4");
set_perm(0, 0, 0644, "/etc/mke2fs.conf");
set_perm(0, 0, 0755, "/tmp/data-backup");
set_perm(0, 0, 0755, "/tmp/data-restore");
set_perm(0, 0, 0755, "/tmp/busybox");
set_progress(0.05);

if
  file_getprop("/tmp/aroma-data/options.prop","option_do_data_backup") == "1"
then
  # Run the modified bonsai backup script
  ui_print("@ Backing up /data");
  ui_print("Backing up /data");
  ui_print("This may take a few minutes");
  run_program("/tmp/data-backup");
  unmount("/data");
  unmount("/sdcard");
  ui_print("Backup complete");
  set_progress(0.1);
endif;

ui_print("@ Formatting /system");
unmount("/system");
run_program("/tmp/fsck.ext4", "-fy", "/dev/block/stl9");
run_program("/tmp/mkfs.ext4", "-O", "^ext_attr,^has_journal,^huge_file", "-L", "SYSTEM", "-b", "4096", "-m", "0", "-F", "/dev/block/stl9");
ui_print("Tuning filesystem");
run_program("/tmp/tune2fs.ext4", "-c", "1", "-m", "0", "-o", "journal_data_writeback", "/dev/block/stl9");
run_program("/tmp/fsck.ext4", "-Dfy", "/dev/block/stl9");
ui_print("Formatting /system complete");
set_progress(0.2);

ui_print("@ Formatting /data");
unmount("/data");
run_program("/tmp/fsck.ext4", "-fy", "/dev/block/stl10");
run_program("/tmp/mkfs.ext4", "-O", "^ext_attr,^has_journal,^huge_file", "-L", "DATA", "-b", "4096", "-m", "0", "-F", "/dev/block/stl10");
ui_print("Tuning filesystem");
run_program("/tmp/tune2fs.ext4", "-c", "1", "-m", "0", "-o", "journal_data_writeback", "/dev/block/stl10");
run_program("/tmp/fsck.ext4", "-Dfy", "/dev/block/stl10");
ui_print("Formatting /data complete");
set_progress(0.25);

ui_print("@ Formatting /cache");
unmount("/cache");
run_program("/tmp/fsck.ext4", "-fy", "/dev/block/stl11");
run_program("/tmp/mkfs.ext4", "-O", "^ext_attr,^has_journal,^huge_file", "-L", "CACHE", "-b", "4096", "-m", "0", "-F", "/dev/block/stl11");
ui_print("Tuning filesystem");
run_program("/tmp/tune2fs.ext4", "-c", "1", "-m", "0", "-o", "journal_data_writeback", "/dev/block/stl11");
run_program("/tmp/fsck.ext4", "-Dfy", "/dev/block/stl11");
ui_print("Formatting /cache complete");
set_progress(0.3);

ui_print("@ Mounting partitions as ext4");
run_program("/sbin/mount", "-t", "ext4", "/dev/block/stl9", "/system");
run_program("/sbin/mount", "-t", "ext4", "/dev/block/stl10", "/data");
run_program("/sbin/mount", "-t", "ext4", "/dev/block/stl11", "/cache");

ui_print("@ Extracting Files");
# show progress takes two arguments
# how much to increment the progress
# and how many files are gonna get transferred.
# 700 is a good default for files transferred on a Gingerbread ROM.
show_progress(0.2, 700);
ui_print("Extracting /system");
package_extract_dir("system", "/system");
ui_print("Extracting /data");
package_extract_dir("data", "/data");
ui_print("Extracting /sdcard");
package_extract_dir("sdcard", "/sdcard");
ui_print("Extracting updates");
package_extract_dir("updates", "/tmp/updates");
set_progress(0.5);

ui_print("@ Setting up toolbox");
symlink("dumpstate","/system/bin/dumpcrash");
symlink("debuggerd", "/system/bin/csview");
symlink("/system/xbin/su", "/system/bin/su");
symlink("toolbox","/system/bin/cat","/system/bin/chmod","/system/bin/chown","/system/bin/cmp","/system/bin/date","/system/bin/dd",
        "/system/bin/df","/system/bin/dmesg","/system/bin/getevent","/system/bin/getprop","/system/bin/hd","/system/bin/id",
        "/system/bin/ifconfig","/system/bin/iftop","/system/bin/insmod","/system/bin/ioctl","/system/bin/ionice","/system/bin/kill",
        "/system/bin/ln","/system/bin/log","/system/bin/ls","/system/bin/lsmod","/system/bin/mkdir","/system/bin/mount",
        "/system/bin/mv","/system/bin/nandread","/system/bin/netstat","/system/bin/newfs_msdos","/system/bin/notify",
        "/system/bin/printenv", "/system/bin/ps","/system/bin/reboot","/system/bin/renice","/system/bin/rm","/system/bin/rmdir",
        "/system/bin/rmmod","/system/bin/route","/system/bin/schedtop","/system/bin/sendevent","/system/bin/setconsole",
        "/system/bin/setprop","/system/bin/sleep","/system/bin/smd","/system/bin/start","/system/bin/stop","/system/bin/sync",
        "/system/bin/top","/system/bin/umount","/system/bin/vmstat","/system/bin/watchprops","/system/bin/wipe");
set_progress(0.55);

ui_print("@ Setting permissions");
set_perm_recursive(0, 0, 0755, 0644, "system");
set_perm_recursive(0, 2000, 0755, 0755, "system/bin");
set_perm_recursive(0, 0, 0755, 0755, "system/etc");
set_perm_recursive(0, 0, 0755, 0755, "system/etc/bluetooth");
set_perm_recursive(1002, 1002, 0755, 0440, "system/etc/bluetooth");
set_perm_recursive(0, 0, 0777, 0777, "system/etc/init.d");
set_perm(0, 0, 0755, "/system/etc/bluetooth");
set_perm(0, 3003, 02755, "/system/bin/netcfg");
set_perm(0, 3004, 02755, "/system/bin/ping");
set_perm(1002, 1002, 0440, "/system/etc/dbus.conf");
set_perm(1014, 2000, 0550, "/system/etc/dhcpcd/dhcpcd-run-hooks");
set_perm(0, 2000, 0550, "/system/etc/init.goldfish.sh");
set_perm(0, 0, 04755, "/system/xbin/su");
set_perm(0, 0, 04755, "/system/xbin/busybox");
set_perm(0, 0, 755, "/tmp/updates/redbend_ua");
set_progress(0.6);

if
  file_getprop("/tmp/aroma-data/options.prop","option_install_modem") == "1"
then
  ui_print("@ Flashing modem");
  run_program("/tmp/updates/redbend_ua", "restore", "/tmp/updates/modem.bin", "/dev/block/bml12");
endif;
set_progress(0.7);

if
  file_getprop("/tmp/aroma-data/options.prop","option_install_kernel") == "1"
then
  ui_print("@ Flashing kernel");
  run_program("/tmp/updates/redbend_ua", "restore", "/tmp/updates/zImage", "/dev/block/bml7");
endif;
  set_progress(0.75);
if
  file_getprop("/tmp/aroma-data/options.prop","option_install_busybox") == "1"
then
  ui_print("@ Installing busybox");
  run_program("system/xbin/busybox", "--install", "-s", "/system/xbin");
endif;
set_progress(0.8);

if
  file_getprop("/tmp/aroma-data/options.prop","option_do_data_backup") == "1"
then
  ui_print("@ Restoring applications and data");
  ui_print("This may take a while");
  run_program("/tmp/data-restore");
  ui_print("Restore completed");
  set_progress(0.9);
endif;

ui_print("@ Unmounting partitions");
run_program("/sbin/umount", "/system");
run_program("/sbin/umount", "/data");
run_program("/sbin/umount", "/cache");
set_progress(1.0);
show_progress(1.0, 0);

ui_print("@ Update complete");

if
  file_getprop("/tmp/aroma-data/options.prop","option_reboot_phone") == "1"
then
  ui_print("@ Rebooting phone");
  run_program("/sbin/reboot");
endif;
